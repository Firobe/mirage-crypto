open Format

let print_header name =
  printf
    {|/*
          Pre-computed multiples of the generator point G for the curve %s,
          used for speeding up its scalar multiplication in point_operations.h.

          Generated by %s, with tables for both 64-bit and 32-bit architectures.
       */|}
    name Sys.argv.(0)

let pp_array elem_fmt fmt arr =
  let fout = fprintf fmt in
  let len = Array.length arr in
  fout "@[<2>{@\n";
  for i = 0 to len - 1 do
    elem_fmt fmt arr.(i);
    if i < len - 1 then printf ",@ " else printf ""
  done;
  fout "@]@,}"

let div_round_up a b = (a / b) + if a mod b = 0 then 0 else 1

let _rev_str_bytes str =
    let len = String.length str in
    let bytes = Bytes.make len '\000' in
    for i = 0 to len - 1 do
        Bytes.set bytes i str.[len - i - 1]
    done;
    bytes

let pp_string_words ~wordsize ~byte_length fmt str =
  let limbs = div_round_up (byte_length * 8) wordsize in
  assert (String.length str * 8 mod wordsize = 0);
  (* Truncate at the beginning (little-endian) *)
  let offset = String.length str mod limbs in
  let bytes = Bytes.unsafe_of_string str in
  (* let bytes = rev_str_bytes str in *)
  fprintf fmt "@[<2>{@\n";
  for i = 0 to limbs - 1 do
    let index = (i * (wordsize / 8)) + offset in
    (if wordsize = 64 then
       let w = Bytes.get_int64_le bytes index in
       fprintf fmt "%#016Lx" w
     else
       let w = Bytes.get_int32_le bytes index in
       fprintf fmt "%#08lx" w);
    if i < limbs - 1 then printf ",@ " else printf ""
  done;
  fprintf fmt "@]@,}"

let check_shape tables =
  let fe_len = String.length tables.(0).(0).(0) in
  let table_len = fe_len * 2 in
  assert (Array.length tables = table_len);
  Array.iter
    (fun x ->
      assert (Array.length x = 15);
      Array.iter
        (fun x ->
          assert (Array.length x = 3);
          Array.iter (fun x -> assert (String.length x = fe_len)) x)
        x)
    tables

let print_tables tables ~wordsize ~byte_length =
  let fe_len = String.length tables.(0).(0).(0) in
  printf "@[<2>static WORD generator_table[%d][15][3][LIMBS] = @," (fe_len * 2);
  pp_array
    (pp_array (pp_array (pp_string_words ~wordsize ~byte_length)))
    std_formatter tables;
  printf "@];@,"

let print_toplevel name (module P : Mirage_crypto_ec.Dh_dsa) =
  let tables, byte_length = P.Dsa.Precompute.generator_tables () in
  check_shape tables;
  print_header name;
  printf "@[<v>#ifdef ARCH_64BIT@,";
  print_tables ~wordsize:64 ~byte_length tables;
  printf "#else // 32-bit@,";
  print_tables ~wordsize:32 ~byte_length tables;
  printf "@]#endif@."

let curves =
  Mirage_crypto_ec.
    [
      ("p224", (module P224 : Dh_dsa));
      ("p256", (module P256));
      ("p384", (module P384));
      ("p521", (module P521));
    ]

let usage () =
  printf "Usage: gen_tables [%a]"
    (pp_print_list
       ~pp_sep:(fun fmt () -> pp_print_string fmt " | ")
       pp_print_string)
    (List.map fst curves)

let go =
  let name, curve =
    try List.find (fun (name, _) -> name = Sys.argv.(1)) curves
    with _ ->
      usage ();
      exit 1
  in
  print_toplevel name curve
